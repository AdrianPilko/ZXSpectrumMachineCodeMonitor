5 POKE 23658,8
6 CLS
10 PRINT INVERSE 1;AT 5,6;"  SPECTRUM MONITOR  "
15 PRINT AT 8,4;"1: ENTER MACHINE CODE"
20 PRINT AT 10,4;"2: EXAMINE MEMORY"
30 PRINT AT 12,4;"3: SAVE BYTES TO TAPE"
40 PRINT AT 14,4;"4: RUN CODE"
70 LET A$=INKEY$: IF A$ < "1" OR A$> "4" THEN GOTO 70
80 CLS : GOSUB 100+200*(VAL A$-1)
90 RUN
100 INPUT "START ADDRESS? ";SA
110 INPUT LINE D$
120 IF D$="" THEN GOTO 110 
125 IF D$(1)="#" THEN RETURN
127 IF LEN D$ = 1 THEN GOTO 110
130 LET S$= D$(1 TO 2)
140 FOR N=1 TO 2
150 IF S$(N)>"9" THEN LET S$(N)=CHR$(CODE S$(N)-7)
155 IF S$(N) > "?" OR S$(N) <"0" THEN PRINT FLASH 1;"INVALID CHARACTER": GOTO 110
160 NEXT N
170 POKE SA, (CODE S$(1)-48)*16+CODE S$(2) - 48
180 PRINT SA,D$( TO 2)
190 LET D$= D$(3 TO )
200 LET SA = SA + 1
210 GOTO 120
300 INPUT "START ADDRESS? ";SA
310 INPUT "PRINTER (Y OR N)? ";LINE P$
320 LET ST=2: IF P$ ="Y" THEN LET ST=3
330 PRINT #ST;SA;
340 FOR M=0 TO 7
350 LET H$="# #"
360 LET H$(1)=CHR$ (INT (PEEK(SA+ M)/16)+ 48)
370 LET H$(2) = CHR$ (48 + PEEK(SA+ M)-16*(CODE H$(1) -48))
380 FOR N=1 TO 2
390 IF H$(N) >"9" THEN LET H$(N)= CHR$ (CODE H$(N) +7)
400 NEXT N
410 PRINT #ST;TAB 7 + 3*M;H$;
420 NEXT M
440 LET SA= SA + 8
445 PRINT #ST: POKE 23692,0
450 LET A$ =INKEY$: IF A$ = "" THEN GOTO 450
460 IF A$ = CHR$ 13 THEN RETURN
470 GOTO 330
500 INPUT "START ADDRESS? ";SA
510 INPUT "NUMBER OF BYTES? ";N
520 INPUT "FILE NAME? "; LINE N$
530 IF LEN N$ <1 OR LEN N$>10 THEN GOTO 520
540 SAVE N$CODE SA,N
550 RETURN
700 INPUT "START ADDRESS TO RUN";SA
730 IF SA<=0 THEN RETURN
740 USR SA
1000 RETURN